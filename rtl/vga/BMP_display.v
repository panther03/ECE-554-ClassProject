
//=======================================================
//  This code is generated by Terasic System Builder
//=======================================================

module BMP_display(
  input clk,
  input rst_n,
  input [9:0] x_pos,
  input x_we,
  input [8:0] y_pos,
  input y_we,
  input [7:0] cmd_in,
  input cmd_we,
  input VGA_CLK,
  output VGA_BLANK_N,
  output VGA_HS,
  output VGA_SYNC_N,
  output VGA_VS,
  output [7:0] VGA_R,
  output [7:0] VGA_G,
  output [7:0] VGA_B,
  output idle
);

  reg [9:0] x_pos_r;
  always @(posedge clk, negedge rst_n) begin
    if (!rst_n)
      x_pos_r <= 0;
    else if (x_we)
      x_pos_r <= x_pos;
    else
      x_pos_r <= x_pos_r;
  end

  reg [8:0] y_pos_r;
  always @(posedge clk, negedge rst_n) begin
    if (!rst_n)
      y_pos_r <= 0;
    else if (y_we)
      y_pos_r <= y_pos;
    else
      y_pos_r <= y_pos_r;
  end

  ////////////////////////////////////
  // internal nets for connections //
  //////////////////////////////////
  wire [18:0] raddr;				// address into videoMem for reads
  wire [8:0] rdata;					// 9-bit color
  wire [18:0] waddr;				// write address to videoMem
  wire [8:0] wdata;					// write data to videoMem
  wire [4:0] image_indx;
  wire we;
  wire add_img,add_fnt;
  wire [5:0] fnt_indx;
  wire [9:0] xloc;
  wire [8:0] yloc;
  
  reg [18:0] count;					// generate a pulse on add_img
 
  ///////////////////////////////////////
  // Instantiate VGA Timing Generator //
  /////////////////////////////////////
  VGA_timing iVGATM(.clk25MHz(VGA_CLK), .rst_n(rst_n), .VGA_BLANK_N(VGA_BLANK_N),
                    .VGA_HS(VGA_HS),.VGA_SYNC_N(VGA_SYNC_N), .VGA_VS(VGA_VS), .addr_lead(raddr));
					
  /////////////////////////////////////
  // Instantiate 9-bit video memory //
  ///////////////////////////////////
  videoMem iVIDMEM(.clk(clk),.we(we),.waddr(waddr),.wdata(wdata),.raddr(raddr),.rdata(rdata));
  
  assign VGA_R = {rdata[8:6],5'b00000};
  assign VGA_G = {rdata[5:3],5'b00000};
  assign VGA_B = {rdata[2:0],5'b00000};
  
  //////////////////////////////////////////////
  // Instantiate Logic that determines pixel //
  // colors based on BMP placement          //
  ///////////////////////////////////////////					
  PlaceBMP iPLCBMP(.clk(clk),.rst_n(rst_n),.add_fnt(add_fnt),.fnt_indx(fnt_indx),
           .add_img(add_img),.rem_img(1'b0),.image_indx(image_indx),
           .xloc(xloc),.yloc(yloc),.waddr(waddr),.wdata(wdata),.we(we),.idle(idle));

  // add fnt or image
  assign add_fnt = cmd_we && ~|cmd_in[1:0];
  assign add_img = cmd_we && cmd_in[1:0] == 2'b01;
  
  // index of bmp
  assign fnt_indx = cmd_in[7:2];
  assign image_indx = cmd_in[6:2];
  
  assign xloc = x_pos_r;
  assign yloc = y_pos_r; 
	
 endmodule